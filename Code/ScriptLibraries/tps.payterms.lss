'++LotusScript Development Environment:2:5:(Options):0:74
Option Public
Option Declare

Use "tps.payterms.rent"

'++LotusScript Development Environment:2:5:(Forward):0:1
Declare Public Class TPS_PaytermsService As FBase_Abstract_Service

'++LotusScript Development Environment:2:5:(Declarations):0:10


Private Const DATABASE_KEY = "tps.contracts.approval"
'======================================================================================================================================
'	CLASS TPS_PaytermsService
'======================================================================================================================================
Public Class TPS_PaytermsService As FBase_Abstract_Service
	Private m_rent As Variant
	Private m_db As NotesDatabase
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	'	PUBLIC:
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Sub New(in_ps As Variant)
	End Sub
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Function getObject( in_link As Variant ) As Variant
		On Error GoTo error_point
		
		Set getObject = Nothing
		If ( in_link.domain Like "tps.payterms.rent.*" ) Then
			Set getObject = Me.rent.getObject( in_link )
		Else
			Error 1001, "The package for object '" & in_link.domain & "' from parameter 'in_link.domain' is not registered"
		End If
		Exit Function
		
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Function
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Property Get rent As Variant
		On Error GoTo error_point
		
		If IsEmpty(m_rent)Then
			Set m_rent = New TPS_Payterms_RentService( Me.Me_Variant )
		End If
		Set rent = m_rent
		Exit Property
		
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Property
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Property Get db As NotesDatabase
		On Error GoTo error_point
		Call Me.log.enter( TypeName( Me ) & "." & GetThreadInfo(1))
		
		If ( m_db Is Nothing )Then
			Set m_db = Me.sm.base.databases.get( DATABASE_KEY )
		End If
		Set db = m_db
		
		Call Me.log.traceObject( "result", db )	
		Call Me.log.exit( TypeName( Me ) & "." & GetThreadInfo(1))
		Exit Property
		
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Property
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Function dialogSelectContracts( in_isMult As Boolean ) As MixCollection
		On Error GoTo error_point
		Call Me.log.enter( "<" & TypeName( Me ) & "." & GetThreadInfo(1) & ">" )
		Call Me.log.parameter( "in_isMult", in_isMult )
		
		Dim mxResult As New MixCollection
		Set dialogSelectContracts = mxResult
		
		Dim vw As NotesView
		Set vw = Me.db.getView( "tps.payterms.contracts" )
		If ( vw Is Nothing ) Then
			Error 1001, "View 'tps.payterms.contracts' is not exist"
		End If
		
		Dim ws As New NotesUIWorkspace, dc As NotesDocumentCollection
		Set dc = ws.PickListCollection( 3, in_isMult, Me.db.Server, Me.db.FilePath, vw.Name, "Список договоров", "Выберите документ(ы) и нажмите  OK" )
		If ( dc.Count > 0 ) Then
			Dim doc As NotesDocument
			Set doc = dc.GetFirstDocument
			Do While Not ( doc Is Nothing )
				Dim c As Variant
				Set c = Me.rent.getDocObject(doc)
				Call mxResult.Append( c )
				Set doc = dc.GetNextDocument( doc )
			Loop
		End If
		
		Call Me.log.exit( "</" & TypeName( Me ) & "." & GetThreadInfo(1) & ">" )
		Exit Function
		
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Function
	
End Class