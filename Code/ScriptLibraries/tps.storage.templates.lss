'++LotusScript Development Environment:2:5:(Options):0:74
Option Public
Option Declare


Use "fbyte"

'++LotusScript Development Environment:2:5:(Forward):0:1
Declare Public Class TPS_Storage_TemplatesService As FBase_Abstract_Service
Declare Class TPS_Storage_Templates_Document As Fbase_abstract_object_doc

'++LotusScript Development Environment:2:5:(Declarations):0:10
Private Const DATABASE_KEY = "tps.storage.templates"

Const DOMAIN_STORAGE_TEMPLATES_DOCUMENT = "tps.storage.templates.document"
'======================================================================================================================================
'	CLASS TPS_Storage_TemplatesService
'======================================================================================================================================
Public Class TPS_Storage_TemplatesService As FBase_Abstract_Service
	Private m_db As NotesDatabase
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	'	PUBLIC:
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Sub New(in_ps As Variant)
	End Sub
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Private Function getId( in_key As Variant ) As String
		On Error GoTo error_point
		Call Me.log.enter( TypeName( Me ) & "." & GetThreadInfo(1))
		
		Dim key As String
		If ( LCase( TypeName( in_key )) = "objectlink" ) Then
			key = in_key.id 
		ElseIf IsScalar( in_key ) Then 
			key = in_key
		Else
			Error 1001, "The type of data of parameter 'in_key' is '" & TypeName( in_key ) & "'. It is not supported"
		End If
		
		getId = key
		
		Call Me.log.trace( "result: " & getId )
		Call Me.log.exit( TypeName( Me ) & "." & GetThreadInfo(1))
		Exit Function
		
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Function
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Function getObject( in_link As Variant ) As Variant
		On Error GoTo error_point
		Call Me.log.enter( TypeName( Me ) & "." & GetThreadInfo(1))
		Call Me.log.parameter( "in_link", in_link )
		
		Select Case in_link.domain
			Case DOMAIN_STORAGE_TEMPLATES_DOCUMENT:
				Set getObject = Me.getDocument( in_link )
			Case Else
				Error 1001, "The data of type '" & in_link.domain & "' of parameter 'in_link.domain' is not supported"
		End Select
		
		Call Me.log.traceObject( "result", getObject )
		Call Me.log.exit( TypeName( Me ) & "." & GetThreadInfo(1))
		Exit Function
		
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Function
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Property Get db As NotesDatabase
		On Error GoTo error_point
		Call Me.log.enter( TypeName( Me ) & "." & GetThreadInfo(1))
		
		If ( m_db Is Nothing )Then
			Set m_db = Me.sm.base.databases.get( DATABASE_KEY )
		End If
		Set db = m_db
		
		Call Me.log.traceObject( "result", db )	
		Call Me.log.exit( TypeName( Me ) & "." & GetThreadInfo(1))
		Exit Property
		
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Property
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Function getDocObject( in_doc As NotesDocument ) As Variant
		On Error GoTo error_point
		Call Me.log.enter( TypeName( Me ) & "." & GetThreadInfo(1))
		Call Me.log.parameter( "in_doc", in_doc )
		
		Select Case LCase( in_doc.form(0))
			Case "document":
				Set getDocObject = New TPS_Storage_Templates_Document( in_doc, Me.Me_variant )
			Case Else:
				Error 1001, "Форма '" & in_doc.form(0) & "' не поддерживается"
		End Select
		
		Call Me.log.traceObject( "result", getDocObject )
		Call Me.log.exit( TypeName( Me ) & "." & GetThreadInfo(1))
		Exit Function
		
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Function
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Private Function getDocument( in_key As Variant ) As Variant
		On Error GoTo error_point
		Call Me.log.enter( TypeName( Me ) & "." & GetThreadInfo(1))
		Call Me.log.parameter( "in_key", in_key )
		
		Dim id As String, doc As NotesDocument
		id = Me.getId( in_key )
		On Error 4091 Resume Next 'bad unid
		Set doc = Me.db.Getdocumentbyunid( id )
		If ( Err > 0 ) Then Err = 0
		
		If ( Not ( doc Is Nothing ) ) Then
			Set getDocument = Me.getDocObject( doc )	
		Else
			Set getDocument = Nothing
		End If
		
		Call Me.log.traceObject( "result", getDocument )
		Call Me.log.exit( TypeName( Me ) & "." & GetThreadInfo(1))
		Exit Function	
		
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$ 
	End Function
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Function dialogSelectDocuments( in_isMult As Boolean ) As Mixcollection
		On Error GoTo error_point
		Call Me.log.enter( "<" & TypeName( Me ) & "." & GetThreadInfo(1) & ">" )
		Call Me.log.parameter( "in_isMult", in_isMult )
		
		Dim mxResult As New MixCollection
		Set dialogSelectDocuments = mxResult
		
		Dim vw As NotesView, viewname As String
		viewname = "App.Document.ByCategory"
		Set vw = Me.db.getView( viewname )
		If ( vw Is Nothing ) Then
			Error 1001, "View '" & viewname & "' is not exist"
		End If
		Dim ws As New NotesUIWorkspace, dc As NotesDocumentCollection
		Set dc = ws.PickListCollection( 3, in_isMult, Me.db.Server, Me.db.FilePath, vw.Name, Me.db.Title, "Выберите документ(ы) и нажмите  OK" )
		If ( dc.Count > 0 ) Then
			Dim doc As NotesDocument
			Set doc = dc.GetFirstDocument
			Do While Not ( doc Is Nothing )
				Dim c As Variant
				Set c = Me.getDocObject(doc)
				Call mxResult.Append( c )
				Set doc = dc.GetNextDocument( doc )
			Loop
		End If

		Call Me.log.exit( "</" & TypeName( Me ) & "." & GetThreadInfo(1) & ">" )
		Exit Function
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Function
	
	
End Class

'======================================================================================================================================
'	CLASS TPS_Storage_Templates_Document
'======================================================================================================================================
Class TPS_Storage_Templates_Document As Fbase_abstract_object_doc
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	'	PUBLIC:
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Sub New( in_doc As NotesDocument, in_ps As Variant )
	End Sub
	'-----------------------------------------------------------------------------------------------------------------------------------------------------------------
	Property Get id As String
		On Error GoTo error_point
		If (m_doc.HasItem( "id" )) Then
			id = m_doc.id(0)
		Else
			id = m_doc.UniversalID
		End If
		Exit Property 
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Property
	'-----------------------------------------------------------------------------------------------------------------------------------------------------------------
	Property Get title As String
		On Error GoTo error_point
		If (m_doc.HasItem( "title" )) Then
			title = m_doc.title(0)
		End If
		Exit Property 
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Property
	'-----------------------------------------------------------------------------------------------------------------------------------------------------------------
	Property Get domain As String
		On Error GoTo error_point
		domain = DOMAIN_STORAGE_TEMPLATES_DOCUMENT
		Exit Property 
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Property
	
End Class