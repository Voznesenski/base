'++LotusScript Development Environment:2:5:(Options):0:74
Option Public
Option Declare

Use "fbyte"
Use "tps.common"


'++LotusScript Development Environment:2:5:(Forward):0:1
Declare Public Class TPS_Timetables_TasksService As FBase_Abstract_Service
Declare Class TPS_Timetables_Tasks_Task As Fbase_abstract_object_doc
Declare Class TPS_Timetables_Tasks_Executor As Fbase_abstract_object_doc

'++LotusScript Development Environment:2:5:(Declarations):0:10
Private Const DATABASE_KEY = "tps.timetables.tasks"

Const DOMAIN_TPS_TIMETABLES_TASKS_TASK = "tps.timetables.task"
Const DOMAIN_TPS_TIMETABLES_TASKS_EXECUTOR = "tps.timetables.task.executor"
'======================================================================================================================================
'	CLASS TPS_Timetables_TasksService
'======================================================================================================================================
Public Class TPS_Timetables_TasksService As FBase_Abstract_Service
	Private m_db As NotesDatabase
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Private Function getId( in_key As Variant ) As String
		On Error Goto error_point
		Call Me.log.enter( Typename( Me ) & "." & Getthreadinfo(1))
		
		Dim key As String
		If ( Lcase( Typename( in_key )) = "objectlink" ) Then
			key = in_key.id 
		Elseif Isscalar( in_key ) Then 
			key = in_key
		Else
			Error 1001, "The type of data of parameter 'in_key' is '" & Typename( in_key ) & "'. It is not supported"
		End If
		
		getId = key
		
		Call Me.log.trace( "result: " & getId )
		Call Me.log.exit( Typename( Me ) & "." & Getthreadinfo(1))
		Exit Function
		
error_point:
		On Error Goto 0
		Error Err, Typename( Me ) & "." & Getthreadinfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Function
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	'	PUBLIC:
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Sub New( in_ps As Variant )
	End Sub
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Property Get db As NotesDatabase
		On Error Goto error_point
		Call Me.log.enter( Typename( Me ) & "." & Getthreadinfo(1))
		
		If( m_db Is Nothing )Then
			Set m_db = Me.sm.base.databases.get( DATABASE_KEY )
		End If
		Set db = m_db
		
		Call Me.log.traceObject( "result", db )		
		Call Me.log.exit( Typename( Me ) & "." & Getthreadinfo(1))
		Exit Property
		
error_point:
		On Error Goto 0
		Error Err, Typename( Me ) & "." & Getthreadinfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Property
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Property Set db As NotesDatabase
		On Error GoTo error_point
		Call Me.log.enter( TypeName( Me ) & "." & GetThreadInfo(1))
		
		Set m_db = db
		
		Exit Property
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Property
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Function getObject( in_link As Variant ) As Variant
		On Error Goto error_point
		Call Me.log.enter( Typename( Me ) & "." & Getthreadinfo(1))
		
		Select Case in_link.domain
		Case DOMAIN_TPS_TIMETABLES_TASKS_TASK:
			Set getObject = Me.getTask( in_link )
		Case DOMAIN_TPS_TIMETABLES_TASKS_EXECUTOR:
			Set getObject = Me.getTask( in_link )	
		Case Else
			Error 1001, "The data of type '" & in_link.domain & "' of parameter 'in_link.domain' is not supported"
		End Select
		
		Call Me.log.traceObject( "result", getObject )
		Call Me.log.exit( Typename( Me ) & "." & Getthreadinfo(1))
		Exit Function
		
error_point:
		On Error Goto 0
		Error Err, Typename( Me ) & "." & Getthreadinfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Function
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------	
	Function getDocObject( in_doc As NotesDocument ) As Variant
		On Error Goto error_point
		Call Me.log.enter( Typename( Me ) & "." & Getthreadinfo(1))
		
		Select Case LCase(in_doc.form(0))
		Case LCase("tps.timetables.task"):
			Set getDocObject =  New TPS_Timetables_Tasks_Task( in_doc, Me.Me_variant )
		Case LCase("tps.timetables.task.executor"):
			Set getDocObject =  New TPS_Timetables_Tasks_Executor( in_doc, Me.Me_variant )
		Case Else:
			Error 1001, "Форма '" & in_doc.form(0) & "' не поддерживается"
		End Select
		
		'Call Me.log.traceObject( "result", getDocObject )
		Call Me.log.exit( Typename( Me ) & "." & Getthreadinfo(1))
		Exit Function
		
error_point:
		On Error Goto 0
		Error Err, Typename( Me ) & "." & Getthreadinfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Function
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Function getTask( in_key As Variant ) As Variant
		On Error GoTo error_point
		Call Me.log.enter( TypeName( Me ) & "." & GetThreadInfo(1))
		Call Me.log.parameter( "in_key", in_key )
		
		Dim id As String, doc As NotesDocument
		id = Me.getId( in_key )
		Set doc = Me.db.Getdocumentbyunid( id )
		Set getTask = Me.getDocObject( doc )
		
		Call Me.log.traceObject( "result", getTask )
		Call Me.log.exit( TypeName( Me ) & "." & GetThreadInfo(1))
		Exit Function	
		
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$ 
	End Function
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Function getTaskExecutor( in_key As Variant ) As Variant
		On Error GoTo error_point
		Call Me.log.enter( TypeName( Me ) & "." & GetThreadInfo(1))
		Call Me.log.parameter( "in_key", in_key )
		
		Dim key As String, vw As NotesView, viewname As String, dc As NotesDocumentCollection
		key = Me.getId( in_key )
		viewname = "(timetable.task.executors.all)"
		Set vw = Me.db.Getview( viewname )
		Set dc = vw.GetAllDocumentsByKey( key, True )
		If ( dc.Count = 0 ) Then
			Set Me.getTaskExecutor = Nothing
		ElseIf ( dc.Count > 1 ) Then
			Error 1001, "In database '" & Me.db.Title & "' ( path: " & Me.db.FilePath & " ) founded more than one doc by key '" & key & "' ( count: " & CStr( dc.Count ) & " )"
		Else
			Set getTaskExecutor = Me.getDocObject( dc.GetFirstDocument )
		End If
		
		Call Me.log.traceObject( "result", getTaskExecutor )
		Call Me.log.exit( TypeName( Me ) & "." & GetThreadInfo(1))
		Exit Function
		
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$ 
	End Function
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Function getTaskExecutors( in_key As String ) As MixCollection
		On Error GoTo error_point
		Call Me.log.enter( TypeName( Me ) & "." & GetThreadInfo(1))
		Call Me.log.parameter( "in_key", in_key )
		
		Dim result As New MixCollection
		Set getTaskExecutors = result
		
		Dim s As New NotesSession, vw As NotesView, viewname As String, dc As NotesDocumentCollection
		viewname = "(timetable.task.executors.all)"
		Set vw = Me.db.Getview( viewname )
		Call vw.Refresh
		Set dc = vw.GetAllDocumentsByKey( in_key, True )
		If ( dc.Count > 0 ) Then
			Dim doc As NotesDocument
			Set doc = dc.GetFirstDocument
			Do While Not ( doc Is Nothing )
				Dim elem As Variant
				Set elem = Me.getDocObject( doc )
				Call result.Append( elem )
				Set doc = dc.GetNextDocument( doc )
			Loop
		End If
		
		Call Me.log.traceObject( "result", getTaskExecutors )
		Call Me.log.exit( TypeName( Me ) & "." & GetThreadInfo(1))
		Exit Function	
		
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$ 
	End Function
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Function createTaskExecutor( in_task As Variant ) As Variant
		On Error GoTo error_point
		Call Me.log.enter( TypeName( Me ) & "." & GetThreadInfo(1))
		Call Me.log.parameter( "in_task", in_task )
		
		Dim s As New NotesSession, db As NotesDatabase, docNew As NotesDocument 
		'Set db = Me.db
		Set db = in_task.nd.ParentDatabase
		
		Set docNew = db.Createdocument()
		Dim exec As New TPS_Timetables_Tasks_Executor(docNew, Me.Me_variant)
		
		Call exec.nd.Replaceitemvalue("Form", "tps.timetables.task.executor")
		Call exec.nd.Replaceitemvalue("domain", DOMAIN_TPS_TIMETABLES_TASKS_EXECUTOR )
		Call exec.nd.Replaceitemvalue("id", exec.nd.Universalid )
		Call exec.nd.Replaceitemvalue("CustomID", exec.nd.Universalid )
		Call exec.nd.Replaceitemvalue("docKind", "ExtraDoc" )
		
		Dim item As NotesItem
		'Set item = in_resolution.nd.Getfirstitem("DWF$Authors")
		'If ( Not item Is Nothing ) Then
		'	Call item.Copyitemtodocument( exec.nd, item.Name )
		'End If
		Set item = in_task.nd.Getfirstitem("DWF$Readers")
		If ( Not item Is Nothing ) Then
			Call item.Copyitemtodocument( exec.nd, item.Name )
		End If
		Set item = in_task.nd.Getfirstitem("DWF$Server")
		If ( Not item Is Nothing ) Then
			Call item.Copyitemtodocument( exec.nd, item.Name )
		End If
		
		Dim docTemp As New NotesDocument( Me.db ), ItemRef As NotesItem
		Call docTemp.Makeresponse( in_task.nd )
		Set ItemRef = docTemp.Getfirstitem("$Ref") 
		If Not ( ItemRef Is Nothing ) Then
			Call ItemRef.Copyitemtodocument( exec.nd, "MainRef" )
		Else
			Error 4001, {Не удалось сформировать поле "$Ref"}
		End If
		
		
		Set createTaskExecutor = exec
		
		
		Call Me.log.traceObject( "result", createTaskExecutor )
		Call Me.log.exit( TypeName( Me ) & "." & GetThreadInfo(1))
		Exit Function	
		
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Function
	
End Class

'======================================================================================================================================
'	CLASS TPS_Timetables_Tasks_Task
'======================================================================================================================================
Class TPS_Timetables_Tasks_Task As Fbase_abstract_object_doc
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	'	PUBLIC:
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Sub New( in_doc As NotesDocument, in_ps As Variant )
	End Sub
	'-----------------------------------------------------------------------------------------------------------------------------------------------------------------
	Property Get id As String
		On Error GoTo error_point
		If (m_doc.HasItem( "id" )) Then
			id = m_doc.id(0)
		Else
			id = m_doc.UniversalID
		End If
		Exit Property 
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Property
	'-----------------------------------------------------------------------------------------------------------------------------------------------------------------
	Property Get title As String
		On Error GoTo error_point
		If (m_doc.HasItem( "title" )) Then
			title = m_doc.title(0)
		End If
		Exit Property 
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Property
	'-----------------------------------------------------------------------------------------------------------------------------------------------------------------
	Property Get domain As String
		On Error GoTo error_point
		domain = DOMAIN_TPS_TIMETABLES_TASKS_TASK
		Exit Property 
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Property
	
End Class

'======================================================================================================================================
'	CLASS TPS_Timetables_Tasks_Executor
'======================================================================================================================================
Class TPS_Timetables_Tasks_Executor As Fbase_abstract_object_doc
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	'	PUBLIC:
	'---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
	Sub New( in_doc As NotesDocument, in_ps As Variant )
	End Sub
	'-----------------------------------------------------------------------------------------------------------------------------------------------------------------
	Property Get id As String
		On Error GoTo error_point
		If (m_doc.HasItem( "id" )) Then
			id = m_doc.id(0)
		Else
			id = m_doc.UniversalID
		End If
		Exit Property 
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Property
	'-----------------------------------------------------------------------------------------------------------------------------------------------------------------
	Property Get title As String
		On Error GoTo error_point
		If (m_doc.HasItem( "title" )) Then
			title = m_doc.title(0)
		End If
		Exit Property 
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Property
	'-----------------------------------------------------------------------------------------------------------------------------------------------------------------
	Property Get domain As String
		On Error GoTo error_point
		domain = DOMAIN_TPS_TIMETABLES_TASKS_EXECUTOR
		Exit Property 
error_point:
		On Error GoTo 0
		Error Err, TypeName( Me ) & "." & GetThreadInfo(1) & " ( nm: " & Err & ", ln: " & Erl & " ) >> " & Error$
	End Property
	
End Class